dnl $CoreSDI: configure.in,v 1.121 2001/04/09 18:32:15 alejo Exp $
dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/syslogd.c)dnl
AC_CONFIG_HEADER(src/config.h)dnl

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Do some AIX stuff before
AC_AIX

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_DIRENT

dnl This will be needed by later ports
dnl AC_HEADER_STDC

AC_HEADER_SYS_WAIT

AC_CHECK_HEADERS(fcntl.h limits.h paths.h strings.h sys/ioctl.h \
    sys/time.h syslog.h unistd.h sysctl.h err.h sys/context.h )dnl

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
dnl It should have strftime!
dnl AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gethostname inet_ntop getaddrinfo regcomp poll select\
 socket strdup strerror strstr strtoul )dnl

dnl 
dnl Here are our checks...
dnl 

if test "x$GCC" = "xyes"
then
	CFLAGS="$CFLAGS -Wall -ansi -pedantic"
fi

MSYSLOG_VERSION="1.04"
AC_DEFINE_UNQUOTED(MSYSLOG_VERSION_STR, "$MSYSLOG_VERSION")dnl
echo msyslog version... $MSYSLOG_VERSION

MLIBNAME="libmsyslog.so.$MSYSLOG_VERSION" ;
AC_DEFINE_UNQUOTED(MLIBNAME_STR, "$MLIBNAME")dnl

MANPAGES="syslog.conf.5 syslogd.8"

if test "x$prefix" = "xNONE"
then
	prefix=$ac_default_prefix
fi
AC_DEFINE_UNQUOTED(INSTALL_LIBDIR, "$prefix/lib/alat")

dnl
dnl Check needed libs for daemon
dnl
AC_CHECK_LIB(dl, dlopen, SYSLOGD_LIBS="$SYSLOGD_LIBS -ldl")dnl
AC_CHECK_LIB(socket, connect, SYSLOGD_LIBS="$SYSLOGD_LIBS -lsocket")dnl
AC_CHECK_LIB(nsl, gethostbyname, SYSLOGD_LIBS="$SYSLOGD_LIBS -lnsl")dnl
AC_DEFINE(SYSLOGD_LIBS, $SYSLOGD_LIBS)dnl

AC_MSG_CHECKING(wheter optreset is needed)
AC_TRY_LINK([#include <unistd.h>],
        [extern int optreset; optreset = 1;],
        [AC_DEFINE(HAVE_OPTRESET) AC_MSG_RESULT(yes)],
        AC_MSG_RESULT(no))

AC_MSG_CHECKING(pidfile directory)

for dir in "/var/run" "/etc"
do
	if test -d $dir ; then
		break
	fi
done
AC_MSG_RESULT(root-mode pid file will go in $dir)
AC_DEFINE_UNQUOTED(PID_DIR, "$dir")dnl

dnl search mandir for non bsd systems
for MANDIR in "/usr/share/man" "/usr/man"
do
	if test -d $MANDIR ; then
		break;
	fi
done

dnl AC_MSG_CHECKING(for MAXHOSTNAMELEN)

AC_CHECK_SIZEOF_DEFINE(MAXHOSTNAMELEN, 254)dnl

dnl Check underscore requirement for dlsym()

AC_DEFINE(SYMBOL_PREFIX, "_")

dnl	AC_MSG_CHECKING([for symbol prefix in compiled objects])

dnl	AC_REQUIRE([AC_PROG_NM])dnl

AC_MSG_CHECKING(wether netdb.h requires _USE_IRS)
if grep _USE_IRS /usr/include/netdb.h >/dev/null ;then
	AC_DEFINE(_USE_IRS)
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
fi

AC_MSG_CHECKING(if sigaltstack uses stack_t structure)
AC_EGREP_HEADER(stack_t, [signal.h],[
	AC_DEFINE(SIGALTSTACK_WITH_STACK_T)
	AC_MSG_RESULT(yes)
], [
	AC_MSG_RESULT(no)
])dnl


UNAME=`uname`
if test "$UNAME" = "OpenBSD" ; then
	AC_DEFINE(DLOPEN_FLAGS, RTLD_LAZY)dnl
	AC_DEFINE_UNQUOTED(SYMBOL_PREFIX, "_")dnl
	SHARED_PARAMS="-Bshareable"
	DCCFLAGS="-fPIC"
elif test "$UNAME" = "Linux" ; then
	AC_DEFINE(DLOPEN_FLAGS, RTLD_LAZY | RTLD_GLOBAL)dnl
	SHARED_PARAMS="-Bshareable"
	DCCFLAGS=""
elif test "$UNAME" = "SunOS" ; then
	AC_DEFINE(DLOPEN_FLAGS, RTLD_LAZY)dnl
	SHARED_PARAMS="-G"
	DCCFLAGS="-fPIC"
elif test "$UNAME" = "IRIX" -o "$UNAME" = "IRIX64" ; then
	AC_DEFINE(DLOPEN_FLAGS, RTLD_LAZY)dnl
	AC_DEFINE(INET6)dnl
	CFLAGS="-D_BSD_SIGNALS"
	if test "x$GCC" == "xyes"
	then
		DCCFLAGS="-Bshared"
	else
		if test "x$MAXOPT" == "xyes"
		then
			DCCFLAGS="$DCCFLAGS -g3 -Ofast"
			CFLAGS="$CFLAGS -g3 -Ofast"
		fi
		SHARED_PARAMS="-shared"
	fi
elif test "$UNAME" = "AIX" ; then
	AC_DEFINE(DLOPEN_FLAGS, RTLD_LAZY | RTLD_GLOBAL)dnl
	AC_DEFINE(NEEDS_DLOPEN_NULL)dnl
	DCCFLAGS="-shared"
	SHARED_PARAMS="-lc -bI:libmsyslog.imp -bexpall -bnoentry -brtl"
	cat > src/modules/libmsyslog.imp <<-EOF
		#! .
		dprintf
		finet
		LogPort
		logerror
		DaemonFlags
		add_fd_input
		LocalHostName
		place_signal
		remove_fd_input
		printline
		EOF

	SYSLOGD_LIBS="$SYSLOGD_LIBS -Wl,-bexpall"
else
	AC_MSG_WARN(cannot determine system type falling to defaults)dnl
	AC_DEFINE(DLOPEN_FLAGS, RTLD_LAZY)dnl
	SHARED_PARAMS="-Bshareable"
fi


dnl
dnl Here we check module support
dnl

AC_ARG_WITH(maximum-optimization,
[  --without-maximum-optimization
	Activate maximum possible compile and link optimization
],[
	if test $with_maximum-optimization = "yes" ; then
		MAXOPT=yes
	else
		MAXOPT=no
	fi
],[
	MAXOPT=no
])

AC_ARG_WITH(bsd,
[  --without-bsd
	Use bsd input module. Automatically detected.
],[
	if test $with_bsd = "yes" ; then
		MSRCS="$MSRCS im_bsd.c"
		AC_DEFINE(HAVE_BSD_IMODULE)
	fi
],[
	if test "$UNAME" = "OpenBSD" -o "$UNAME" = "NetBSD" \
	    -o "$UNAME"  = "FreeBSD" -o "$UNAME" = "BSDi" \
	    -o "$UNAME"  = "386BSD"  -o "$UNAME" = "ArchBSD" \
	    -o "$UNAME"  = "TrustedBSD" ; then
		MSRCS="$MSRCS im_bsd.c"
		AC_DEFINE(HAVE_BSD_IMODULE)
	fi
])dnl


AC_ARG_WITH(linux,
[  --without-linux
	Use linux kernel input module. Automatically detected.
],[
	if test "$with_linux" = "yes" ; then
		MSRCS="$MSRCS im_linux.c"
		MANPAGES="$MANPAGES im_linux.8"
		AC_DEFINE(HAVE_LINUX_IMODULE)
	fi
],[
	AC_CHECK_HEADER(sys/klog.h, [
		MSRCS="$MSRCS im_linux.c"
		MANPAGES="$MANPAGES im_linux.8"
		AC_DEFINE(HAVE_LINUX_IMODULE)
	])dnl
])dnl

AC_ARG_WITH(udp,
[  --without-udp
	Use udp socket input module. Activated by default.
],[
	if test $with_udp = "yes" ; then
		MSRCS="$MSRCS im_udp.c"
	fi
],[
	AC_CHECK_HEADER(sys/socket.h, [
		MSRCS="$MSRCS im_udp.c"
	])dnl
])dnl

temp_res="no";

AC_ARG_WITH(unix,
[  --without-unix
	Use unix socket domain input module. Activated by default.
],[ temp_res="$with_unix"; ],[
	AC_CHECK_HEADER(sys/un.h, [ temp_res="yes"; ])dnl
])dnl
if test "X$temp_res" = "Xyes" ; then
		MSRCS="$MSRCS im_unix.c"
		AC_DEFINE(HAVE_UNIX_IMODULE)
		MANPAGES="$MANPAGES im_unix.8"
fi

dnl Doors should only be used with streams

temp_res=no
AC_ARG_WITH(streams,
[  --without-streams
	Use streams input module. Activated by default.
],[

	if test "$with_streams" = "yes" ; then
		temp_res="yes"
	fi
],[
 	AC_CHECK_HEADER(sys/strlog.h, strlog_h="yes", strlog_h="no")dnl
 	AC_CHECK_HEADER(door.h, door_h="yes", door_h="no")dnl
	if test "$strlog_h" = "yes" -a "$door_h" = "yes" ; then
		temp_res="yes"
	fi
])

if test "$temp_res" = "yes" ; then
	MSRCS="$MSRCS im_streams.c"
	MLIBS="$MLIBS -lpthread"
	MANPAGES="$MANPAGES im_streams.8"
	SYSLOGD_LIBS="$SYSLOGD_LIBS -lthread -ldoor"
	AC_DEFINE(HAVE_STREAMS_IMODULE)
fi
AC_MSG_CHECKING(for streams module)
AC_MSG_RESULT($temp_res)

temp_res=no
AC_ARG_WITH(classic,
[  --without-classic
	Use classic output module. Activated by default.
],[ temp_res="$with_unix"; ],[ temp_res="yes"; ],[
])
if test "X$temp_res" = "Xyes" ; then
	MSRCS="$MSRCS om_classic.c ttymsg.c"
	MANPAGES="$MANPAGES om_classic.8"
fi

AC_ARG_WITH(tcp,
[  --without-tcp
	Use tcp input and output modules. Activated by default.
],[
	if test $with_tcp = "yes" ; then
		MSRCS="$MSRCS om_tcp.c im_tcp.c ip_misc.c"
		MANPAGES="$MANPAGES om_tcp.8 im_tcp.8"
		temp_res="yes"
	else
		temp_res="no"
	fi
],[
	MSRCS="$MSRCS om_tcp.c im_tcp.c ip_misc.c"
	MANPAGES="$MANPAGES om_tcp.8 im_tcp.8"
	temp_res="yes"
])

if test "$temp_res" = "yes" ; then
	AC_EGREP_HEADER(sa_len, [#include <sys/socket.h> ],
	    AC_DEFINE(HAVE_SOCKADDR_SA_LEN))
fi

AC_ARG_WITH(mysql,
[  --without-mysql
	Use mysql MySQL output module. Activated by default.
],[
	if test $with_mysql = "yes" ; then
		MSRCS="$MSRCS om_mysql.c"
		MANPAGES="$MANPAGES om_mysql.8"
	fi
],[
	MSRCS="$MSRCS om_mysql.c"
	MANPAGES="$MANPAGES om_mysql.8"
	USESQL="yes"
])

AC_ARG_WITH(pgsql,
[  --without-pgsql
	Use pgsql PostgreSQL output module. Activated by default.
],[
	if test $with_pgsql = "yes" ; then
		MSRCS="$MSRCS om_pgsql.c"
		MANPAGES="$MANPAGES om_pgsql.8"
	fi
],[
	MSRCS="$MSRCS om_pgsql.c"
	MANPAGES="$MANPAGES om_pgsql.8"
	USESQL="yes"
])

if test "$USESQL" = "yes" ; then
	MSRCS="$MSRCS sql_misc.c"
fi

AC_ARG_WITH(peo,
[  --without-peo
	Use peo log integrity verification module. Activated by default.
],[
	if test $with_peo = "yes" ; then
		PEO=yes
	else
		PEO=no
	fi
],[
	PEO=yes
])

HASH_SRCS=""
if test $PEO = "yes" ; then

	MSRCS="$MSRCS om_peo.c"
	MANPAGES="$MANPAGES om_peo.8 peochk.8"

	HASH_SRCS="hash.c"
	AC_CHECK_FUNC(MD5Init, AC_DEFINE(HAVE_MD5),
	        HASH_SRCS="$HASH_SRCS md5c.c")
	AC_CHECK_FUNC(SHA1Init, AC_DEFINE(HAVE_SHA1),
	        HASH_SRCS="$HASH_SRCS sha1.c")
	AC_CHECK_FUNC(RMD160Init, AC_DEFINE(HAVE_RMD160),
	        HASH_SRCS="$HASH_SRCS rmd160.c")
	changequote(<<, >>)dnl
	HASH_SRCS_MODULES=`echo $HASH_SRCS | sed 's/[0-9a-zA-Z.]*/..\/peo\/&/g'`
	HASH_OBJS_MODULES=`echo $HASH_SRCS | sed 's/\.c/.o/g'`
	changequote([, ])dnl
	if test -r /dev/srandom ; then
		AC_DEFINE(HAVE_SRANDOM)
	fi
fi

AC_ARG_WITH(regex,
[  --without-regex
	Use regex pattern matching output module. Activated by default.
],[
	if test $with_regex = "yes" ; then
		MSRCS="$MSRCS om_regex.c"
		MANPAGES="$MANPAGES om_regex.8"
	fi
],[
	AC_CHECK_HEADER(regex.h, [
		MSRCS="$MSRCS om_regex.c"
		MANPAGES="$MANPAGES om_regex.8"
	])dnl
])

AC_CACHE_SAVE

AC_HAVE_TYPES(socklen_t uint32_t uint64_t u_int32_t u_int64_t \
	__uint32_t __uint64_t CODE)dnl

dnl check CODE structure typedef
dnl AC_CHECK_CODE_TYPE()dnl 

AC_SUBST(SYSLOGD_LIBS)dnl
AC_SUBST(SHARED_PARAMS)dnl
AC_SUBST(DCCFLAGS)dnl
AC_SUBST(MSRCS)dnl
AC_SUBST(MLIBS)dnl
AC_SUBST(MLIBNAME)dnl
AC_SUBST(MANPAGES)dnl
AC_SUBST(HASH_SRCS)dnl
AC_SUBST(HASH_SRCS_MODULES)dnl
AC_SUBST(HASH_OBJS_MODULES)dnl
AC_SUBST(HAVE_SOCKLEN_T)dnl

dnl AC_SUBST(SYMBOL_PREFIX)dnl

AC_OUTPUT([Makefile
	    src/Makefile
	    src/modules/Makefile
	    src/peo/Makefile
	    src/man/Makefile
	    src/man/BSDmakefile
	    src/man/GNUmakefile])dnl
