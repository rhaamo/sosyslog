dnl	$CoreSDI: configure.in,v 1.17.2.8.2.3 2000/09/11 21:15:30 alejo Exp $

AC_INIT(src/syslogd.h)
AC_CONFIG_HEADER(src/config.h)

MSYSLOG_VERSION=`grep '#define VERSION' src/syslogd.h | cut -f 3`
echo msyslog version... $MSYSLOG_VERSION

AC_PROG_CC
AC_PROG_INSTALL
AC_PREFIX_DEFAULT(/usr/local)

CFLAGS="-O2"
LDFLAGS=
LDDFLAGS=
DCCFLAGS="-fPIC"
LDCONFIG_FLAGS="-m"
UNAME=`uname`


MSYSLOG_CHECK_MSYSLOG_LIBDIR()
AC_DEFINE_UNQUOTED(INSTALL_LIBDIR, "$MSYSLOG_LIBDIR")

AC_CHECK_HEADERS(paths.h)
AC_MSG_CHECKING(wheter optreset is needed)
AC_TRY_LINK([#include <unistd.h>],
	[extern int optreset; optreset = 1;],
	[AC_DEFINE(HAVE_OPTRESET) AC_MSG_RESULT(yes)],
	AC_MSG_RESULT(no))

for dir in "/var/run" "/etc"
do
	if test -d $dir
	then
		break
	fi
done
AC_MSG_RESULT(root-mode pid file will go in $dir)
AC_DEFINE_UNQUOTED(PID_DIR, "$dir")

dnl search mandir for non bsd systems
for MANDIR in "/usr/share/man" "/usr/man"
do
	if test -d $MANDIR
	then
		break;
	fi
done


HASH_SRCS="hash.c"
AC_CHECK_FUNC(MD5Init, AC_DEFINE(HAVE_MD5),
	HASH_SRCS="$HASH_SRCS md5c.c")
AC_CHECK_FUNC(SHA1Init, AC_DEFINE(HAVE_SHA1),
	HASH_SRCS="$HASH_SRCS sha1.c")
AC_CHECK_FUNC(RMD160Init, AC_DEFINE(HAVE_RMD160),
	HASH_SRCS="$HASH_SRCS rmd160.c")


if test "$UNAME" = "OpenBSD"
then
	echo Configuring for OpenBSD system
	AC_DEFINE(HAVE_OPENBSD)
elif test "$UNAME" = "Linux"
then
	echo Configuring for Linux system
	LIBS="$LIBS -ldl"
	DCCFLAGS=
	LDDFLAGS="-Xlinker -E"
	AC_DEFINE(HAVE_LINUX)
fi

MSYSLOG_BEGIN_MODULES_CONF()
MSYSLOG_CHECK_MODULE(unix, $UNIX, im, im_unix.c)
MSYSLOG_CHECK_MODULE(udp, $UDP, im, im_udp.c)
MSYSLOG_CHECK_MODULE(bsd, $BSD, im, im_bsd.c)
MSYSLOG_CHECK_MODULE(linux, $LINUX, im, im_linux.c)
MSYSLOG_CHECK_MODULE(classic, $CLASSIC, om, om_classic.c)
MSYSLOG_CHECK_MODULE(peo, $PEO, om, om_peo.c)

if test "x$PEO" = "xstatic"
then
	SHASH_SRCS="$HASH_SRCS"
elif test "x$PEO" = "xdynamic"
then
	DHASH_SRCS="$HASH_SRCS"
fi

dnl Both SQL cant be static
if test "x$MYSQL" != "x" -a "x$PGSQL" != "x"
then
	AC_MSG_ERROR("You can not use more than one SQL module right now");
fi

if test "x$MYSQL" = "xstatic" -o "x$MYSQL" = "xdynamic"
then
	MSYSLOG_CHECK_MYSQL()
fi
MSYSLOG_CHECK_MODULE(mysql, $MYSQL, om, om_mysql.c, sql_misc.c)

dnl Add to LIBS and CPP params
if test "x$MYSQL" = "xstatic"
then
	LIBS="$LIBS $MYSQL_LIBS"
	CPPFLAGS="$CPPFLAGS $MYSQL_CPPFLAGS"
fi

if test "x$PGSQL" = "xstatic" -o "x$PGSQL" = "xdynamic"
then
	MSYSLOG_CHECK_PGSQL()
fi

dnl Add to LIBS and CPP params
if test "x$PGSQL" = "xstatic"
then
	LIBS="$LIBS $PGSQL_LIBS"
	CPPFLAGS="$CPPFLAGS $PGSQL_CPPFLAGS"
fi

MSYSLOG_CHECK_MODULE(pgsql, $PGSQL, om, om_pgsql.c, sql_misc.c)
MSYSLOG_CHECK_MODULE(regex, $REGEX, om, om_regex.c)
MSYSLOG_END_MODULES_CONF()

AC_CHECK_TYPE_HDR()

dnl
dnl Determine which manpages should be installed
dnl

MANPAGES="syslog.conf.5 syslogd.8"

if test "x$LINUX" = "xstatic" -o "x$LINUX" = "xdynamic"
then
	MANPAGES="$MANPAGES im_linux.8"
fi
if test "x$PEO" = "xstatic" -o "x$PEO" = "xdynamic"
then
	MANPAGES="$MANPAGES om_peo.8 peochk.8"
fi
if test "x$REGEX" = "xstatic" -o "x$REGEX" = "xdynamic"
then
	MANPAGES="$MANPAGES om_regex.8"
fi
if test "x$MYSQL" = "xstatic" -o "x$MYSQL" = "xdynamic"
then
	MANPAGES="$MANPAGES om_mysql.8"
fi
if test "x$PGSQL" = "xstatic" -o "x$PGSQL" = "xdynamic"
then
	MANPAGES="$MANPAGES om_pgsql.8"
fi
dnl end manpage stuff

AC_SUBST(MSYSLOG_LIBDIR)
AC_SUBST(HASH_SRCS)
AC_SUBST(SHASH_SRCS)
AC_SUBST(DHASH_SRCS)
AC_SUBST(LDCONFIG_FLAGS)
AC_SUBST(LIBS)
AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(DCCFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(LDDFLAGS)
AC_SUBST(UNAME)
AC_SUBST(MANDIR)
AC_SUBST(MYSQL_CPPFLAGS)
AC_SUBST(MYSQL_LIBS)
AC_SUBST(PGSQL_CPPFLAGS)
AC_SUBST(PGSQL_LIBS)
AC_SUBST(MSYSLOG_VERSION)
AC_SUBST(MANPAGES)

AC_OUTPUT([Makefile
	   src/Makefile src/static_modules.h src/static_modules.c
	   src/modules/Makefile
	   src/peo/Makefile
	   src/man/BSDmakefile
	   src/man/GNUmakefile])

dnl Give final message
echo -e "\n ***************************************\n"\
	"***************************************\n"\
	"*                                     *\n"\
	"* Msyslog was properly configured!    *\n"\
	"*                                     *\n"\
	"* Now you can compile it with         *\n"\
	"* make                                *\n"\
	"* or compile and install with         *\n"\
	"* make install                        *\n"\
	"*                                     *\n"\
	"* Make sure the file modules.conf     *\n"\
	"* meets your needs. If not, modify    *\n"\
	"* it to meet your needs and run       *\n"\
	"* again ./configure                   *\n"\
	"*                                     *\n"\
	"* For more info please read INSTALL   *\n"\
	"* and README files.                   *\n"\
	"*                                     *\n"\
	"***************************************\n"\
	"***************************************\n"

